

global proc
AEBE_VDBRemoveDivergenceTemplate( string $nodeAttr )
{
    editorTemplate -beginScrollLayout;
    {

            editorTemplate -beginLayout "BE_VDB RemoveDivergence" -collapse 0;
            editorTemplate -beginNoOptimize;
            {

            editorTemplate -callCustom "newBE_VDBRemoveDivergenceGridSelection" "replaceBE_VDBRemoveDivergenceGridSelection" "VdbAllGridNamesRemDiv";
            
            editorTemplate -addSeparator;
            
            editorTemplate -addControl "Iterations";

            }
        editorTemplate -endNoOptimize;
        editorTemplate -endLayout;
        editorTemplate -addExtraControls;
		
    }
    editorTemplate -endScrollLayout;
}

global proc newBE_VDBRemoveDivergenceGridSelection(string $attr)
{
    optionMenu
        -label "VDB Grid Name:"
        -width 300
        -changeCommand ("updateBE_VDBRemoveDivergenceGridSelection( \""+$attr+"\" )")
        vdbGridNameMenuRemDiv;

    replaceBE_VDBRemoveDivergenceGridSelection($attr);
}

global proc replaceBE_VDBRemoveDivergenceGridSelection(string $attr)
{
    //connectControl vdbGridNameMenu $attr;
    
	$node = plugNode($attr);
    // fix changeCommand
    optionMenu -e -changeCommand ("updateBE_VDBRemoveDivergenceGridSelection( \""+$attr+"\" )") vdbGridNameMenuRemDiv;

    // save current item
    string $citem = getAttr ($node+".VdbSelectedGridNamesRemDiv"); 
    
    // Clear old items
    {
        string $items[] = `optionMenu -q -ill vdbGridNameMenuRemDiv`;
        string $item;
        for ($item in $items) deleteUI $item;
    }

    // Add new items
    {
        string $currentGridNames = `getAttr $attr`;
        $currentGridNames = "* " + $currentGridNames;

        string $gridNames[];
        tokenize $currentGridNames " " $gridNames;

        string $name;
        for ($name in $gridNames) menuItem -l $name -parent vdbGridNameMenuRemDiv;
    }
    
    // restore current item
	if(`size($citem)` > 0)
    {
     optionMenu -e -value $citem vdbGridNameMenuRemDiv;
	}   
	
    /// @todo re-select previous item if it exists, don't update VdbSelectedGridNames if the same item is selectd.

}

global proc updateBE_VDBRemoveDivergenceGridSelection(string $attr)
{
    string $selectedGrid = `optionMenu -q -value vdbGridNameMenuRemDiv`;
    string $selectionAttr = plugNode($attr) + ".VdbSelectedGridNamesRemDiv"; 
    setAttr -type "string" $selectionAttr $selectedGrid;
}
